;###################################
; Poker cards 2.5 x 3.5 - 825 x 1125
; Generates Scrapbots deck suitable
; for upload to GameCrafter/TTS
;###################################
UNIT=INCH
PAGE=8.5,11,PORTRAIT,HV
CARDSIZE=2.75,3.75
BORDER=NONE
DPI=300

;ZOOM = 83

; dataset
LINK = "scrapbots.csv", name, type, cost, energy, attack1cost, attack1, attack2cost, attack2, health, component, resource, wound, image, amount

; vars
[count] = 20
[scrap-icon] = "images/art/auto-repair.png"
[health-icon] = "images/art/techno-heart.png"
[energy-icon] = "images/art/battery-pack.png"

; name
FONT = Share Tech Mono, 16, T, #000
TEXT = 1-[count], "[name]", .35, -1.5, 100%, 100%, left

; type
FONT = Share Tech Mono, 12, T, #696969
TEXT = 1-[count], "Type: [type]", .35, -1.3, 100%, 100%, left

; cost
IF = [cost] <> "0"
  FONT = Share Tech Mono, 22, T, #000
  TEXT = 1-[count], [cost], -.6, -1.45, 100%, 100%, right
  IMAGE = 1-[count], [scrap-icon], 2.15, .25, .35, .35, 0, T
ENDIF

; art
IMAGE = 1-[count], [image], .9, .7, 35%, 35%, 0, TP

; health
IF = [health] <> "0"
  IMAGE = 1-[count], [health-icon], 1.45, 2.3, .3, .3, 0, T
  FONT = Share Tech Mono, 24, T, #000
  TEXT = 1-[count], "[health]", 1.1, 1.97, 1, 1, left
ENDIF

; attack1
IF = [attack1] <> "~"
  RECTANGLE = 1-[count], .25, 2.6, 82%, .4, #e2e2e2, #e2e2e2
  FONT = Roboto Mono, 6, T, #000
  TEXT = 1-[count], "([attack1cost] Energy)", .35, 2.6, 78%, 100%, right, wordwrap
  PATTERN = 1-[count], [energy-icon], [attack1cost], .35, 2.47, 80%, 10%, .1, .1, 0, T, left, center
  FONT = Roboto Mono, 8, T, #000
  TEXT = 1-[count], "Attack: [attack1]", .35, 2.7, 78%, 100%, left, wordwrap
ENDIF

; attack2
IF = [attack2] <> "~"
  RECTANGLE = 1-[count], .25, 3.1, 82%, .4, #e2e2e2, #e2e2e2
  FONT = Roboto Mono, 6, T, #000
  TEXT = 1-[count], "([attack2cost] Energy)", .35, 3.1, 78%, 100%, right, wordwrap
  PATTERN = 1-[count], [energy-icon], [attack2cost], .35, 2.97, 80%, 10%, .1, .1, 0, T, left, center
  FONT = Roboto Mono, 8, T, #000
  TEXT = 1-[count], "Attack: [attack2]", .35, 3.2, 78%, 100%, left, wordwrap
ENDIF

; resource
IF = [resource] <> "~"
  RECTANGLE = 1-[count], .25, 2.5, 82%, .5, #e2e2e2, #e2e2e2
  FONT = Roboto Mono, 8, T, #000
  TEXT = 1-[count], "[resource]", .35, 2.53, 78%, 100%, left, wordwrap
ENDIF

; component
IF = [component] <> "~"
  RECTANGLE = 1-[count], .25, 2.5, 82%, .5, #e2e2e2, #e2e2e2
  FONT = Roboto Mono, 8, T, #000
  TEXT = 1-[count], "Activate: [component]", .35, 2.53, 78%, 100%, left, wordwrap
ENDIF

; wound
IF = [wound] <> "~"
  RECTANGLE = 1-[count], .25, 2.5, 82%, .7, #e2e2e2, #e2e2e2
  FONT = Roboto Mono, 8, T, #000
  TEXT = 1-[count], "[wound]", .3, 2.53, 75%, 100%, left, wordwrap
ENDIF

; energy
IF = [energy] <> "0"
 ;PATTERN = 1-[count], [energy-icon], [energy], .025, 1.45, 100%, 100%, .25, .25, 0, T, center, center

  FONT = Share Tech Mono, 22, T, #000
  TEXT = 1-[count], [energy], 0, 3.25, 50%, 10%, right
  IMAGE = 1-[count], [energy-icon], 1.35, 3.3, .25, .25, 0, T
ENDIF

; border
RECTANGLE = 1-[count], .19, .19, 2.38, 3.38, #000000, empty, .01

; Bleed lines: Comment the lines below before uploading
;RECTANGLE=1-[count],0.125,0.125,2.5,3.5,#FF0000,empty,0.01
;RECTANGLE=1-[count],0.25,0.25,2.25,3.25,#0000FF,empty,0.01