;##################################
;Poker cards 2.5 x 3.5 - 825 x 1125
;##################################
UNIT=INCH
CARDSIZE=2.75,3.75
BORDER=NONE
DPI=300

; dataset
LINK = "scrapbots.csv", name, type, cost, energy, attack1cost, attack1, attack2cost, attack2, health, component, tactic, wound, image

; vars
[count] = 18
[scrap-icon] = "images/art/auto-repair.png"
[tactic-icon] = "images/art/digital-trace.png"
[overdrive-icon] = "images/art/overdrive.png"
[health-icon] = "images/art/techno-heart.png"
[energy-icon] = "images/art/battery-pack.png"
[attack-icon] = "images/art/cogsplosion.png"

; name
FONT = Share Tech Mono, 16, T, #000
TEXT = 1-[count], "[name]", .35, -1.5, 100%, 100%, left

; type
FONT = Share Tech Mono, 10, T, #696969
TEXT = 1-[count], "Type: [type]", .35, -1.3, 100%, 100%, left

; cost
IF = [cost] <> "0"
  FONT = Share Tech Mono, 22, T, #000
  TEXT = 1-[count], [cost], -.6, -1.45, 100%, 100%, right
  IMAGE = 1-[count], [scrap-icon], 2.15, .25, .35, .35, 0, T
ENDIF

; art
IMAGE = 1-[count], [image], .9, 1, 1.1, 1.1, T

; health
IF = [health] <> "0"
  IMAGE = 1-[count], [health-icon], 2.2, 2.2, .3, .3, 0, T
  FONT = Share Tech Mono, 24, T, #000
  TEXT = 1-[count], "[health]", 1.82, 1.87, 1, 1, left
ENDIF

; attack1
IF = [attack1] <> "~"
  RECTANGLE = 1-[count], .25, 2.5, 82%, .3, #e2e2e2, #e2e2e2
  IMAGE = 1-[count], [attack-icon], .4, 2.55, .2, .2, 0, T
  PATTERN = 1-[count], [energy-icon], [attack1cost], .75, 2.4, 80%, 10%, .1, .1, 0, T, left, center
  FONT = Roboto Mono, 4, T, #000
  TEXT = 1-[count], "[attack1]", .75, 2.65, 60%, 100%, left, wordwrap
ENDIF

; attack2
IF = [attack2] <> "~"
  RECTANGLE = 1-[count], .25, 2.9, 82%, .3, #e2e2e2, #e2e2e2
  IMAGE = 1-[count], [attack-icon], .4, 2.95, .2, .2, 0, T
  PATTERN = 1-[count], [energy-icon], [attack2cost], .75, 2.8, 80%, 10%, .1, .1, 0, T, left, center
  FONT = Roboto Mono, 4, T, #000
  TEXT = 1-[count], "[attack2]", .75, 3.05, 60%, 100%, left, wordwrap
ENDIF

; tactic
IF = [tactic] <> "~"
  RECTANGLE = 1-[count], .25, 2.5, 82%, .3, #e2e2e2, #e2e2e2
  IMAGE = 1-[count], [tactic-icon], .4, 2.55, .2, .2, 0, T
  FONT = Roboto Mono, 4, T, #000
  TEXT = 1-[count], "[tactic]", .75, 2.53, 60%, 100%, left, wordwrap
ENDIF

; component
IF = [component] <> "~"
  RECTANGLE = 1-[count], .24, 3.1, 82%, .2, #e2e2e2, #e2e2e2
  ;IMAGE = 1-[count], [overdrive-icon], .5, 7.2, .5, .5, 0, T
  FONT = Roboto Mono,4, T, #000
  TEXT = 1-[count], "[component]", .4, -.5, 75%, 100%, left, wordwrap, 180
ENDIF

; wound
IF = [wound] <> "~"
  RECTANGLE = 1-[count], .25, 2.5, 82%, .3, #e2e2e2, #e2e2e2
  FONT = Roboto Mono,4, T, #000
  TEXT = 1-[count], "[wound]", .3, 2.53, 75%, 100%, left, wordwrap
ENDIF

; energy
IF = [energy] <> "0"
  RECTANGLE = 1-[count], .24, 3.3, 82%, .2, #e2e2e2, #e2e2e2
  PATTERN = 1-[count], [energy-icon], [energy], -.3, 1.53, 100%, 100%, .2, .2, 180, T, right, center
ENDIF

;Comment the lines below before uploading
;RECTANGLE=1-[count],.19,.19,2.345,3.5,#FFF,empty,.1
RECTANGLE=1-[count],0.125,0.125,2.5,3.5,#FF0000,empty,0.01
RECTANGLE=1-[count],0.25,0.25,2.25,3.25,#0000FF,empty,0.01