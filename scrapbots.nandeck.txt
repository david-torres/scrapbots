;###################################
; Poker cards 2.5 x 3.5 - 825 x 1125
; Generates Scrapbots deck suitable
; for upload to GameCrafter/TTS
;###################################
UNIT=INCH
PAGE=8.5,11,PORTRAIT,HV
CARDSIZE=2.75,3.75
BORDER=NONE
DPI=300

;ZOOM = 83

; dataset
LINK = "scrapbots.csv", name, type, cost, energy, attack1cost, attack1, attack2cost, attack2, health, component, tactic, wound, image, amount

; vars
[count] = 18
[scrap-icon] = "images/art/auto-repair.png"
[tactic-icon] = "images/art/digital-trace.png"
[health-icon] = "images/art/techno-heart.png"
[energy-icon] = "images/art/battery-pack.png"
[attack-icon] = "images/art/cogsplosion.png"

; name
FONT = Share Tech Mono, 16, T, #000
TEXT = 1-[count], "[name]", .35, -1.5, 100%, 100%, left

; type
FONT = Share Tech Mono, 12, T, #696969
TEXT = 1-[count], "Type: [type]", .35, -1.3, 100%, 100%, left

; cost
IF = [cost] <> "0"
  FONT = Share Tech Mono, 22, T, #000
  TEXT = 1-[count], [cost], -.6, -1.45, 100%, 100%, right
  IMAGE = 1-[count], [scrap-icon], 2.15, .25, .35, .35, 0, T
ENDIF

; art
IMAGE = 1-[count], [image], .9, .7, 35%, 35%, 0, TP

; health
IF = [health] <> "0"
  IMAGE = 1-[count], [health-icon], 2.2, 1.9, .3, .3, 0, T
  FONT = Share Tech Mono, 24, T, #000
  TEXT = 1-[count], "[health]", 1.82, 1.57, 1, 1, left
ENDIF

; attack1
IF = [attack1] <> "~"
  RECTANGLE = 1-[count], .25, 2.2, 82%, .4, #e2e2e2, #e2e2e2
  ;IMAGE = 1-[count], [attack-icon], .4, 2.55, .2, .2, 0, T
  PATTERN = 1-[count], [energy-icon], [attack1cost], .35, 2.07, 80%, 10%, .1, .1, 0, T, left, center
  FONT = Roboto Mono, 8, T, #000
  TEXT = 1-[count], "[attack1]", .35, 2.3, 78%, 100%, left, wordwrap
ENDIF

; attack2
IF = [attack2] <> "~"
  RECTANGLE = 1-[count], .25, 2.7, 82%, .4, #e2e2e2, #e2e2e2
  ;IMAGE = 1-[count], [attack-icon], .4, 2.95, .2, .2, 0, T
  PATTERN = 1-[count], [energy-icon], [attack2cost], .35, 2.57, 80%, 10%, .1, .1, 0, T, left, center
  FONT = Roboto Mono, 8, T, #000
  TEXT = 1-[count], "[attack2]", .35, 2.8, 78%, 100%, left, wordwrap
ENDIF

; tactic
IF = [tactic] <> "~"
  RECTANGLE = 1-[count], .25, 2.5, 82%, .7, #e2e2e2, #e2e2e2
  IMAGE = 1-[count], [tactic-icon], .4, 2.55, .2, .2, 0, T
  FONT = Roboto Mono, 8, T, #000
  TEXT = 1-[count], "Play: [tactic]", .75, 2.53, 60%, 100%, left, wordwrap
ENDIF

; component
IF = [component] <> "~"
  RECTANGLE = 1-[count], .24, 2.8, 82%, .5, #e2e2e2, #e2e2e2
  HTMLFONT = roboto, "Roboto Mono", 8,, #000, left
  HTMLMARGINS = roboto, 0, 0, 0, 0, top
  HTMLTEXT = 1-[count], "[name] Overdrive: [component]", .3, -.5, 78%, 100%, #000, 180, EB, 100, roboto
ENDIF

; wound
IF = [wound] <> "~"
  RECTANGLE = 1-[count], .25, 2.5, 82%, .7, #e2e2e2, #e2e2e2
  FONT = Roboto Mono, 8, T, #000
  TEXT = 1-[count], "[wound]", .3, 2.53, 75%, 100%, left, wordwrap
ENDIF

; energy
IF = [energy] <> "0"
  RECTANGLE = 1-[count], .24, 3.3, 82%, .2, #e2e2e2, #e2e2e2
  PATTERN = 1-[count], [energy-icon], [energy], -.3, 1.53, 100%, 100%, .2, .2, 180, T, right, center
ENDIF

RECTANGLE = 1-[count], .2, .2, 2.35, 3.35, #000000, empty, .1

;Comment the lines below before uploading
;RECTANGLE=1-[count],0.125,0.125,2.5,3.5,#FF0000,empty,0.01
;RECTANGLE=1-[count],0.25,0.25,2.25,3.25,#0000FF,empty,0.01